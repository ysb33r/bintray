// ============================================================================
// (C) Copyright Schalk W. Cronje 2013-2014
//
// This software is licensed under the Apache License 2.0
// See http://www.apache.org/licenses/LICENSE-2.0 for license details
//
// Unless required by applicable law or agreed to in writing, software distributed under the License is
// distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and limitations under the License.
//
// ============================================================================

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.ysb33r.gradle:grolifant:0.5.1'
        classpath 'io.github.http-builder-ng:http-builder-ng-okhttp:1.0.3'
//        classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
//        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:2.2.3'
    }
}

// Now we can load the plugin script

plugins {
    id 'groovy'
    id 'maven'
    id 'com.gradle.plugin-publish' version '0.9.10'
    id 'com.github.hierynomus.license' version '0.14.0'
}

version = '2.0-SNAPSHOT'
group = 'org.ysb33r.gradle'
sourceCompatibility = 1.7
targetCompatibility = 1.7

apply plugin: new GroovyScriptEngine(
        [file('src/main/groovy').absolutePath, file('src/main/resources').absolutePath].toArray(new String[2]),
        this.class.classLoader
).loadScriptByName('org/ysb33r/gradle/bintray/BintrayPublishPlugin.groovy')

ext {
    bintrayOwner = 'ysb33r'
    bintrayRepo  = 'grysb33r'
}

repositories {
    jcenter()
}

configurations.all {
    exclude module : 'groovy-all'
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'io.github.http-builder-ng:http-builder-ng-okhttp:1.0.3'
    compile 'org.ysb33r.gradle:grolifant:0.5.1'
    testCompile 'com.github.rest-driver:rest-client-driver:1.1.37'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    description "An archive of the JavaDocs for Maven Central"
    classifier "javadoc"
    from javadoc
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

uploadArchives  {
    repositories {
        bintrayMavenDeployer {
            username    project.properties.bintrayUserName
            apiKey      project.properties.bintrayApiKey
            repoOwner   bintrayOwner
            repoName    bintrayRepo
            packageName project.name
            description 'This is a bintray publish plugin for Gradle'
            tags        'gradle','bintray'
            licenses          'Apache-2.0'
            vcsUrl            'https://github.com/ysb33r/bintray.git'
            autoCreatePackage true
            updatePackage     true
            versionAttributes 'gradle-plugin' : "org.ysb33r.bintray:${project.group}:${project.name}"

        }
    }
}

pluginBundle {
    description = 'This is a bintray publish plugin for Gradle'
    website     = 'https://github.com/ysb33r/bintray'
    vcsUrl      = 'https://github.com/ysb33r/bintray.git'

    tags = ['gradle','bintray']

    plugins {
        vfsPlugin {
            id = 'org.ysb33r.bintray'
            displayName = 'Unofficial Gradle Bintray plugin - works with classic Maven plugin'
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
    }
}

publishPlugins {
    onlyIf { !version.endsWith("SNAPSHOT") }
}

license {
    strictCheck = true
    ignoreFailures = false
    ext.year = '2013-2018'
    header = rootProject.file('config/apache-header')
}