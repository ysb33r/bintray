// ============================================================================
// (C) Copyright Schalk W. Cronje 2013
//
// This software is licensed under the Apache License 2.0
// See http://www.apache.org/licenses/LICENSE-2.0 for license details
//
// Unless required by applicable law or agreed to in writing, software distributed under the License is
// distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and limitations under the License.
//
// ============================================================================

apply plugin: 'groovy'
apply plugin: 'maven'

group = 'org.ysb33r.gradle'
archivesBaseName = 'bintray'

ext {
    moduleName = 'bintray-gradle-plugin'
}

repositories {
    jcenter()
    mavenCentral()
}

configurations.all {
    exclude module : 'groovy-all'
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile ('org.codehaus.groovy.modules.http-builder:http-builder:0.7.1') {
        exclude group: 'org.apache.httpcomponents', module:'httpclient'
        exclude group: 'org.apache.httpcomponents', module: 'httpcore'
    }
    compile 'org.apache.httpcomponents:httpcore:4.3'
    compile 'org.apache.httpcomponents:httpclient:4.3'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'com.github.rest-driver:rest-client-driver:1.1.37'
}

jar {
    manifest {
        attributes 'Implementation-Title': moduleName, 'Implementation-Version': project.version
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

   task javadocJar(type: Jar) {
        description "An archive of the JavaDocs for Maven Central"
        classifier "javadoc"
        from javadoc
    }

artifacts {
    archives sourcesJar
    archives javadocJar
}

if(version.endsWith("SNAPSHOT")) {
    apply plugin: 'artifactory'
    artifactory {
        publish {
            contextUrl = 'http://oss.jfrog.org'
            repository {
                repoKey = 'oss-snapshot-local'
                username = project.properties.bintrayUserName
                password = project.properties.bintrayApiKey
            }
        }
    }
} else {
    uploadArchives  {
        repositories {
            mavenLocal()
            bintrayMavenDeployer {
                username    project.properties.bintrayUserName
                apiKey      project.properties.bintrayApiKey
                repoOwner   bintrayOwner
                repoName    bintrayRepo
                packageName moduleName
                description 'This is a bintray publish plugin for Gradle'
                tags        'gradle','bintray'
                licenses          'Apache-2.0'
                vcsUrl            'https://github.com/ysb33r/bintray.git'
                autoCreatePackage true
                updatePackage     true
                versionAttributes 'gradle-plugin' : "org.ysb33r.bintray:${group}:${archivesBaseName}"

            }
        }
    }
}

apply from : '../gradle/publish.gradle'